plugins {
	id 'org.springframework.boot' version '2.5.5'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'fr.brouillard.oss.gradle.jgitver' version '0.9.1'
	id 'java'
	id 'jacoco'
}

group = 'com.ultimalabs'

configurations {
	developmentOnly
	runtimeClasspath {
		extendsFrom developmentOnly
	}
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

task cleanDeployDir(type: Delete) {
	delete fileTree('provisioning/roles/deploy-app/files') {
		include '**/*.jar'
	}
}

task buildDeployJar(type: Copy) {
	from bootJar
	into "provisioning/roles/deploy-app/files"
}

jar {
	enabled = false
}

dependencies {
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.apache.commons:commons-text:1.8'
	implementation 'javax.ws.rs:javax.ws.rs-api:2.0'
	implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.11.1'
	compileOnly 'org.projectlombok:lombok:1.18.20'
	annotationProcessor 'org.projectlombok:lombok:1.18.20'
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
}

test {
	useJUnitPlatform()
}

buildDeployJar.dependsOn cleanDeployDir, build
test.finalizedBy jacocoTestReport
